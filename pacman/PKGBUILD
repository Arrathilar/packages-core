# Based on the file created for Arch Linux by:
# Dan McGee <dan@archlinux.org>
# Dave Reisner <dreisner@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Rob McCathie <rob@manjaro.org>

pkgname=pacman
pkgver=4.2.1
pkgrel=4
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.042-2' 'glibc>=2.17-2' 'libarchive>=3.1.2' 'curl>=7.39.0'
         'gpgme' 'pacman-mirrorlist>=20130418' 'archlinux-keyring' 'manjaro-keyring')
checkdepends=('python2' 'fakechroot')
makedepends=('asciidoc')
optdepends=('haveged: for pacman-init.service')
provides=('pacman-contrib' 'pacman-init')
conflicts=('pacman-contrib' 'pacman-init')
replaces=('pacman-contrib' 'pacman-init')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=('strip' 'debug')
source=(https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz
        pacman.conf.i686
        pacman.conf.x86_64
        makepkg.conf
        ensure-matching-database-and-package-version.patch
        #pacman-updated-locales.patch
        pacman-sync-first-option.patch
        etc-pacman.d-gnupg.mount
        pacman-init.service)

prepare() {
  cd $srcdir/$pkgname-$pkgver

  # Archlinux patches
  patch -p1 -i $srcdir/ensure-matching-database-and-package-version.patch

  # Manjaro patches
  patch -p1 -i $srcdir/pacman-sync-first-option.patch
  #patch -p1 -i $srcdir/pacman-updated-locales.patch

  # fix bad translation mistake - corrected in transifex
  sed -i 's#pacman-db-update#pacman-db-upgrade#' src/pacman/po/nl.po

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-ldconfig=/usr/bin/ldconfig
  #rm $srcdir/$pkgname-$pkgver/lib/libalpm/po/*gmo
  #cd $srcdir/$pkgname-$pkgver/lib/libalpm/po/
  #make update-gmo
  #rm $srcdir/$pkgname-$pkgver/scripts/po/*gmo
  #cd $srcdir/$pkgname-$pkgver/scripts/po/
  #make update-gmo
  #rm $srcdir/$pkgname-$pkgver/src/pacman/po/*gmo
  #cd $srcdir/$pkgname-$pkgver/src/pacman/po/
  #make update-gmo
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make
  make -C contrib
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf
  
  case "$CARCH" in
    i686)    
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686"
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64"
      ;;
  esac
  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  # set things correctly in the default conf file
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
    
  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"

  # replace rankmirrors
  rm "$pkgdir/usr/bin/rankmirrors"
  ln -sfv "/usr/bin/pacman-mirrors" "$pkgdir/usr/bin/rankmirrors"

  # install pacman-init
  install -dm755 $pkgdir/usr/lib/systemd/system/
  install -m644 $srcdir/etc-pacman.d-gnupg.mount $pkgdir/usr/lib/systemd/system/etc-pacman.d-gnupg.mount
  install -m644 $srcdir/pacman-init.service $pkgdir/usr/lib/systemd/system/pacman-init.service
}

sha256sums=('a142593c409ef21f6fde0075c7649a58a8ee0edbd2af3f82e4dc05cfc1d0b545'
            '40a3914d2f04dad7835e8ef64c8e0ad4c8cf4e1d664b29156765c7c4a1fe2f69'
            '6fc6a198e853f7bff64ddef853edf2779d7d6f8518a9d3fda620d99513114850'
            'a9f1d448ca8876ec6efc92c4f40ff384ad000c827914ad3a841acf5d2af50b28'
            'c4510fa2fdc68379ecb5e75795f232b37695b1d7eb72444476a16f8be48ea313'
            'feb18bf548343ce6bd60b1e9252ad4b0643c21ac1a53149d93afbd6028a7a37d'
            'b6d14727ec465bb66d0a0358163b1bbfafcb4eaed55a0f57c30aabafae7eed68'
            'ca4aa967ccfb9affab23fd61ad6d16f330b2810e5f9de93deec49ea7872369ae')
