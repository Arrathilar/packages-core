# Based on the file created for Arch Linux by:
# Dan McGee <dan@archlinux.org>
# Dave Reisner <dreisner@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Rob McCathie <rob@manjaro.org>

pkgname=pacman
pkgver=5.0.1
pkgrel=9
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme'
        'archlinux-keyring' 'manjaro-keyring' 'pacman-mirrors>=4.0.0')
checkdepends=('python2' 'fakechroot')
makedepends=('asciidoc')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=('etc/pacman.conf' 'etc/makepkg.conf')
options=('strip' 'debug')
source=("https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"
        'pacman.conf.i686'
        'pacman.conf.x86_64'
        'makepkg.conf'
#         'pacman-sync-first-option.patch'
#         'etc-pacman.d-gnupg.mount'
#         'pacman-init.service'
        'pacman-5.0.1-roundup.patch')
sha256sums=('8bd5f407ce8e05c4be8f1c4be4d8dcc8550ea5e494937da5220ea2c23cbb8e04'
            'b06c1f507a713867d76c2c257e97cce818ffda255a03a157b447563ccc3e29ed'
            'efc3de56605b3523d022cca1d34b4feaac0a5a43da3ffcc63ea4a361e0f5e73d'
            '56a016d6a5404ab6dd7b8fd39027ef422dd14ad07c768853863a1e03dcc9a3e0'
            'e9d65724f1784026dfe94b8038012bb2f4988abeed993b83307537fd93a7e695')

prepare() {
    cd "$pkgname-$pkgver"

    # v5.0.1..pacman-5.0.1..c2f97580
    patch -p1 -i $srcdir/pacman-5.0.1-roundup.patch

#     # Manjaro patches
#     patch -p1 -i $srcdir/pacman-sync-first-option.patch
}

build() {
    cd "$pkgname-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --enable-doc \
        --with-scriptlet-shell=/usr/bin/bash \
        --with-ldconfig=/usr/bin/ldconfig
    make V=1
    make -C contrib
}

check() {
    make -C "$pkgname-$pkgver" check
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" -C contrib install

    # install Arch specific stuff
    install -dm755 "$pkgdir/etc"
    install -m644 "$srcdir/pacman.conf.$CARCH" "$pkgdir/etc/pacman.conf"

    case $CARCH in
        i686)
            mycarch="i686"
            mychost="i686-pc-linux-gnu"
            myflags="-march=i686"
        ;;
        x86_64)
            mycarch="x86_64"
            mychost="x86_64-pc-linux-gnu"
            myflags="-march=x86-64"
        ;;
    esac

    # set things correctly in the default conf file
    install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
    sed -i "$pkgdir/etc/makepkg.conf" \
        -e "s|@CARCH[@]|$mycarch|g" \
        -e "s|@CHOST[@]|$mychost|g" \
        -e "s|@CARCHFLAGS[@]|$myflags|g"

    # put bash_completion in the right location
    install -dm755 "$pkgdir/usr/share/bash-completion/completions"
    mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
    rmdir "$pkgdir/etc/bash_completion.d"

    for f in makepkg pacman-key; do
        ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
    done

    install -Dm644 contrib/PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"

    # replace rankmirrors
    rm "$pkgdir/usr/bin/rankmirrors"
    ln -sfv "/usr/bin/pacman-mirrors" "$pkgdir/usr/bin/rankmirrors"

#     # install pacman-init
#     install -dm755 $pkgdir/usr/lib/systemd/system/
#     install -m644 $srcdir/etc-pacman.d-gnupg.mount $pkgdir/usr/lib/systemd/system/etc-pacman.d-gnupg.mount
#     install -m644 $srcdir/pacman-init.service $pkgdir/usr/lib/systemd/system/pacman-init.service
}
