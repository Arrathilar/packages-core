# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.13
pkgrel=0
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
makedepends=('systemd')
depends=('ncurses' 'libsystemd')
source=("https://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz"
        'topdefaultrc')
sha256sums=('52b05b2bd5b05f46f24766a10474337ebadd828df9915e2b178df291cf88f7d3'
            '918526626ddbdb3e75eef6a3ce5bef809473609e45576454acaa28c9749017a4')

groups=('base')

conflicts=('procps' 'sysvinit-tools')
provides=('procps' 'sysvinit-tools')
replaces=('procps' 'sysvinit-tools')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's:<ncursesw/:<:g' -i watch.c
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--enable-watch8bit \
		--with-systemd \
		--disable-kill

	# kill is provided by util-linux

	make
}

package() {
	install -Dm644 topdefaultrc "$pkgdir"/etc/topdefaultrc

	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
