# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Contributor: Fabian Bornschein <plusfabi[at]gmail[dog]com>

pkgname=('mhwd-amdgpu')
pkgver=1.2.0
pkgrel=1

pkgdesc="MHWD module-ids for amdgpu"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
source=("https://raw.githubusercontent.com/pciutils/pciids/master/pci.ids")
md5sums=('SKIP')

prepare() {
    # Names of supported hardware generations
    HARDWARENAMES=("Bristol Ridge"
                    "Raven Ridge"
                    "Stoney Ridge"
                    "Fiji" "Tonga"
                    "Amethyst"
                    "Polaris"
                    "Polaris10"
                    "Polaris11"
                    "Polaris12"
                    "Baffin"
                    "Ellesmere")


    # Check for file
    if [ ! -f "${srcdir}/pci.ids" ] || ! grep "List of PCI ID's" "${srcdir}/pci.ids" > /dev/null
        then
            echo "Missing or wrong source PCIID-file"
            exit 1
        fi

    ## Extract AMD part
    # Remove everything before ^1002
    sed -i -n '/^1002/,$p' "${srcdir}/pci.ids"
    # Remove everything below ^1003
    sed -i '/^1003/,$d' "${srcdir}/pci.ids"
    # Remove comments
    sed -i '/^#/ d' "${srcdir}/pci.ids"
    # Leading tab
    sed -i 's/^\t//g' "${srcdir}/pci.ids"
    # Remove sub-IDs
    sed -i '/^\t/ d' "${srcdir}/pci.ids"

    # Create the final output file
    echo -e "# List generated by mhwd. Do not edit manually." > "${srcdir}/amdgpu.ids"

    # Loop this part for every Hardware generation
    for (( i=0; i<${#HARDWARENAMES[@]}; i++ ));
    do
        # Ignore generation without entry
        if grep -wi " ${HARDWARENAMES[i]}" "${srcdir}/pci.ids" > /dev/null
            then
                echo "+${HARDWARENAMES[i]}"
                # Add all the ID's
                grep -wi " ${HARDWARENAMES[i]}" "${srcdir}/pci.ids" | grep -vi "audio" | grep -v "#" | cut -d" " -f1 >> "${srcdir}/amdgpu.ids"
        fi
    done

}

package() {
    install -d -m755 "${pkgdir}/var/lib/mhwd/ids/pci/"
    install -D -m644 "${srcdir}/amdgpu.ids" \
    "${pkgdir}/var/lib/mhwd/ids/pci/amdgpu.ids"
}
