# Maintainer: Frede Hundewadt <fh@manjaro.org>
# Contributor: Philip MÃ¼ller <philm@manjaro.org>

_branch=4.2.x-stable
_date=$(date +%Y%m%d)
pkgname=pacman-mirrors
pkgver=4.2.0
pkgrel=3
pkgdesc="Manjaro Linux mirror list for use by pacman"
arch=('any')
depends=('python' 'python-npyscreen')
makedepends=('git' 'python-babel' 'python-setuptools')
optdepends=('gtk3: for interactive mode (GUI)'
            'python-gobject: for interactive mode (GUI)')
url="https://github.com/manjaro/pacman-mirrors.git"
conflicts=('pacman-mirrorlist' 'pacman-mirrorlist-dev')
replaces=('pacman-mirrorlist' 'pacman-mirrorlist-dev')
provides=("pacman-mirrorlist=$_date")
license=('GPL')
backup=(etc/pacman-mirrors.conf)
source=(git+$url#branch=$_branch
        'pacman-mirrors-install.script'
        'pacman-mirrors-upgrade.script'
        'pacman-mirrors-install.hook'
        'pacman-mirrors-upgrade.hook')
sha256sums=('SKIP'
            '661f153cdb0e6b0de1f513c8df92e8e986a083d37b4abd09658fa1e4673a72ea'
            '3b1df8c662161903653b0ae41d910019f87a58f3ecd8e02ea9ac8859b9c43f17'
            '5f6bef6ea223b2c6dcf1334b2fa143c73833e95000152cc00b4b32a46e315450'
            'f33115dc921af39d56990ce6da190fb671528d7a35ecabb07fbbec2060987291')

prepare() {
  cd "${srcdir}"/pacman-mirrors
  # do something here
}

package() {
  cd "${srcdir}"/pacman-mirrors
  make DESTDIR="${pkgdir}" install

  install -D ${srcdir}/pacman-mirrors-install.script ${pkgdir}/usr/share/libalpm/scripts/pacman-mirrors-install
  install -D ${srcdir}/pacman-mirrors-upgrade.script ${pkgdir}/usr/share/libalpm/scripts/pacman-mirrors-upgrade
  install -Dm644 ${srcdir}/pacman-mirrors-install.hook ${pkgdir}/usr/share/libalpm/hooks/pacman-mirrors-install.hook
  install -Dm644 ${srcdir}/pacman-mirrors-upgrade.hook ${pkgdir}/usr/share/libalpm/hooks/pacman-mirrors-upgrade.hook
}
